# Prefixes
PREFIX mayor: <http://example.org/mayor/>
PREFIX nyc: <http://example.org/nyc/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema1: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# 1. Simple triple pattern matching
# CQ1: Retrieve all mayors and their birth dates.
SELECT ?mayor ?name ?birthDate
WHERE {
    ?mayor a schema1:Person ;
           rdfs:label ?name ;
           schema1:birthDate ?birthDate .
}

# 2. Filtering using logical & numerical operators
# CQ2: Mayors born after 1940 who have a term starting after 2000.
# (Using safe string comparison due to xsd:date issues in Protégé)
SELECT ?mayor ?name ?birth ?term ?start
WHERE {
    ?mayor a schema1:Person ;
           rdfs:label ?name ;
           schema1:birthDate ?birth ;
           schema1:hasOccupation ?term .
    ?term schema1:startDate ?start .

    FILTER(STR(?birth) > "1940-01-01")
    FILTER(STR(?start) > "2000-01-01")
}
ORDER BY ?start

# 3. OPTIONAL usage
# CQ3: Retrieve mayors and their political party (if available).
SELECT ?mayor ?name ?birthDate ?partyName
WHERE {
    ?mayor a schema1:Person ;
           rdfs:label ?name ;
           schema1:birthDate ?birthDate .
    OPTIONAL {
        ?mayor schema1:affiliation ?party .
        ?party rdfs:label ?partyName .
    }
}

# 4. Aggregation (COUNT)
# CQ4: Count how many mayors belong to each political party.
SELECT ?partyName (COUNT(?mayor) AS ?numMayors)
WHERE {
    ?mayor schema1:affiliation ?party .
    ?party rdfs:label ?partyName .
}
GROUP BY ?partyName

# 5. Graph traversal (3+ hops)
# CQ5: Successor of the predecessor of a mayor born after 1950.
SELECT ?mayor ?name ?successor2Name
WHERE {
    ?mayor a schema1:Person ;
           rdfs:label ?name ;
           schema1:birthDate ?birthDate .
    ?mayor mayor:precededBy ?pred1 .
    ?pred1 mayor:precededBy ?pred2 .
    ?pred2 mayor:succeededBy ?successor2 .
    ?successor2 rdfs:label ?successor2Name .

    FILTER(STR(?birthDate) > "1950-01-01")
}
